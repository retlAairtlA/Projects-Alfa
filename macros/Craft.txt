$${

TOGGLE(sleep_macro);
    IF(sleep_macro);
        LOG("&2&l[Macro] &6Sleep_Craft &2[ON]");
        SET(&item_stack,"");
        UNSET(start);
        GUI(INVENTORY);
        GETSLOT(371,#slot_pepita,9);
        GETSLOT(266,#slot_ouro,9);
        GETSLOT(264,#slot_diamante,9);
        GETSLOT(265,#slot_ferro,9);
        GETSLOT(388,#slot_esmeralda,9);
        GETSLOT(351,#slot_lapis,9);
        GETSLOT(360,#slot_melancia,9);
        GETSLOT(406,#slot_quartzo,9);
            IF(%#slot_pepita%>=9);
                SET(#id_minerio,"371");
                SET(&name_minerio,"&e&lPEPITA");
                SET(start);
                SET(&item_stack,"%&item_stack%-");
            ENDIF;
            IF(%#slot_ouro%>=9);
                SET(#id_minerio,"266");
                SET(&name_minerio,"&e&lOURO");
                SET(start);
                SET(&item_stack,"%&item_stack%-");
            ENDIF;
            IF(%#slot_diamante%>=9);
                SET(#id_minerio,"264");
                SET(&name_minerio,"&b&lDIAMANTE");
                SET(start);
                SET(&item_stack,"%&item_stack%-");
            ENDIF;
            IF(%#slot_ferro%>=9);
                SET(#id_minerio,"265");
                SET(&name_minerio,"&f&lFERRO");
                SET(start);
                SET(&item_stack,"%&item_stack%-");
            ENDIF;
            IF(%#slot_esmeralda%>=9);
                SET(#id_minerio,"388");
                SET(&name_minerio,"&2&lESMERALDA");
                SET(start);         
                SET(&item_stack,"%&item_stack%-");
            ENDIF;
            IF(%#slot_lapis%>=9);
                SET(#id_minerio,"351");
                SET(&name_minerio,"&1&lLápis-Lazúli");
                SET(start);
                SET(&item_stack,"%&item_stack%-");
            ENDIF;
            IF(%#slot_melancia%>=9);
                SET(#id_minerio,"360");
                SET(&name_minerio,"&c&lMELANCIA");
                SET(start);
                SET(&item_stack,"%&item_stack%-");
            ENDIF;      
            IF(%#slot_quartzo%>=9);
                SET(#id_minerio,"406");
                SET(&name_minerio,"&f&lQUARTZO");
                SET(start);
                SET(&item_stack,"%&item_stack%-");
                SET(#sleep_packs_min,"4");
            ELSE;
                SET(#sleep_packs_min,"9");
            ENDIF;
            IF(start);
                IFMATCHES("%&item_stack%","^--(.*)$");
                    LOG("&e» &7Você deve deixar apenas &9um &7tipo de item para craftar. &9&o#SLEEP");
                    GUI();
                    WAIT(10T);
                    LOG("&4&l[Macro] &6Sleep_Craft &4[OFF]");
                    TOGGLE(sleep_macro);
                    STOP;         
                ELSE;
                    SET(#sleep_delay,"");
                    SET(#item_pack,"");
                    SET(#getslot,"9");      
                        DO;
                            GUI(INVENTORY);
                            GETSLOT(%#id_minerio%,#sleep_slot,%#getslot%);
                            GETSLOTITEM(%#sleep_slot%,#sleep_id,#sleep_stack);
                                IF((%#sleep_slot%>=%#getslot%)&&(%#sleep_stack%=64));
                                    INC(#item_pack,1);
                                    #getslot = %#sleep_slot% + 1;
                                ELSE;
                                    IF(%#sleep_slot%!=-1);
                                        #getslot = %#sleep_slot% + 1;
                                    ENDIF;
                                ENDIF;
                        UNTIL((%#sleep_slot%=-1)||(%#item_pack%>=%#sleep_packs_min%));                
                        IF(%#item_pack%>=%#sleep_packs_min%);
                                IF(%#id_minerio%=406);
                                    GUI(INVENTORY);
                                ELSE;
                                    LOG("&e» &7Aguardando Bancada de Trabalho... &9&o#SLEEP");
                                    SET(#sleep_shield,"100");
                                    GUI();
                                        DO;
                                                IF(%#sleep_shield%>=100);
                                                    IF(%HITID%=58);
                                                        KEY(USE);
                                                        SET(#sleep_shield,"");
                                                    ELSE;
                                                        ECHO("/bench");
                                                        SET(#sleep_shield,"");
                                                    ENDIF;
                                                ENDIF;
                                            INC(#sleep_shield,1);
                                        UNTIL(%GUI%="GUICRAFTING");
                                    WAIT(5T);
                                    LOG("&e» &7Crafting %&name_minerio%&7's. &9&o#SLEEP");
                                ENDIF;
                            SET(guicrafting);
                                DO;
                                        DO;
                                            UNSAFE;
                                                FOR(#sleep_slot,1,%#sleep_packs_min%);
                                                    GETSLOTITEM(%#sleep_slot%,#sleep_id,#sleep_stack);
                                                        IF(%#sleep_id%=0);
                                                            SET(#craft_vazio,"%#sleep_slot%");
                                                            SET(&craft_vazio,"LOADING");
                                                            BREAK;
                                                        ELSE;
                                                            SET(&craft_vazio,"CONTINUE");
                                                        ENDIF;
                                                NEXT;
                                                IF(%&craft_vazio%="LOADING");
                                                    IF(%#id_minerio%=406);
                                                        SET(#getslot,"9");
                                                    ELSE;
                                                        SET(#getslot,"10");
                                                    ENDIF;
                                                    DO;
                                                        RANDOM(#random_slot,%#sleep_packs_min%,1);
                                                        GETSLOTITEM(%#random_slot%,#sleep_id,#sleep_stack);
                                                            IF(%#sleep_id%!=0);
                                                                SET(#random_slot,"%#craft_vazio%");
                                                            ENDIF;
                                                            DO;
                                                                GETSLOT(%#id_minerio%,#sleep_slot,%#getslot%);
                                                                GETSLOTITEM(%#sleep_slot%,#sleep_id,#sleep_stack);
                                                                #getslot = %#sleep_slot% + 1;                                                       
                                                            UNTIL(%#sleep_stack%=64);
                                                        SLOTCLICK(%#sleep_slot%,L,FALSE);
                                                        WAIT(%#sleep_delay%MS);
                                                        SLOTCLICK(%#random_slot%,L,FALSE);      
                                                        WAIT(%#sleep_delay%MS);
                                                        GETSLOTITEM(%#random_slot%,#sleep_id,#sleep_stack);                                             
                                                    UNTIL(%#random_slot%!=0);
                                                ENDIF;                          
                                            ENDUNSAFE;
                                        UNTIL(%&craft_vazio%="CONTINUE");
                                    SET(#sleep_shield,"");
                                    SET(#craft_c,"");
                                    UNSET(error);
                                        DO;
                                            GETSLOTITEM(0,#sleep_id,#sleep_stack);
                                                IFMATCHES("%#sleep_id%","^(41|266|57|42|133|22|103|155)$");
                                                    INC(#craft_c,1);
                                                ELSE;
                                                    INC(#sleep_shield,1);
                                                        IF(%#sleep_shield%>=30);
                                                            INC(#sleep_delay,20);
                                                            SET(error);
                                                            BREAK;
                                                        ENDIF;
                                                ENDIF;
                                                IF(%#craft_c%>=7);
                                                    SLOTCLICK(0,L,TRUE);
                                                    WAIT(1T);
                                                    BREAK;                              
                                                ENDIF;
                                        LOOP;
                                        IF(%#id_minerio%=406);
                                            IF(%GUI%!="GUIINVENTORY");
                                                UNSET(guicrafting);
                                                BREAK;
                                            ENDIF;
                                        ELSE;
                                            IF(%GUI%!="GUICRAFTING");
                                                UNSET(guicrafting);
                                                BREAK;
                                            ENDIF;
                                        ENDIF;
                                        IF(error);
                                            DO;
                                                GETSLOT(%#id_minerio%,#sleep_slot,1);
                                                    IF((%#sleep_slot%<=%#sleep_packs_min%)&&(%#sleep_slot%!=-1));
                                                        SLOTCLICK(%#sleep_slot%,L,TRUE);
                                                        WAIT(1T);
                                                    ENDIF;
                                            UNTIL((%#sleep_slot%>=%#sleep_packs_min%)||(%#sleep_slot%=-1));                          
                                        ELSE;
                                            SET(#item_pack,"");
                                                IF(%#id_minerio%=406);
                                                    SET(#getslot,"9");
                                                ELSE;
                                                    SET(#getslot,"10");
                                                ENDIF;
                                                DO;
                                                    GETSLOT(%#id_minerio%,#sleep_slot,%#getslot%);
                                                    GETSLOTITEM(%#sleep_slot%,#sleep_id,#sleep_stack);
                                                        IF((%#sleep_slot%>=%#getslot%)&&(%#sleep_stack%=64));
                                                            INC(#item_pack,1);
                                                            #getslot = %#sleep_slot% + 1;
                                                        ELSE;
                                                            IF(%#sleep_slot%!=-1);
                                                                #getslot = %#sleep_slot% + 1;
                                                            ENDIF;
                                                        ENDIF;
                                                UNTIL((%#sleep_slot%=-1)||(%#item_pack%>=%#sleep_packs_min%));
                                        ENDIF;
                                WHILE(%#item_pack%>=%#sleep_packs_min%);
                                IF(guicrafting);
                                    ECHO("/kit");
                                        DO(100);
                                        UNTIL(%GUI%="GUICHEST");
                                    PRESS(E);
                                    WAIT(1T);
                                    LOG("&e» &7FIM. &9&o#SLEEP");
                                ELSE;
                                    IF(%#id_minerio%=406);
                                        LOG("&e» &7Inventário fechado. &9&o#SLEEP");
                                    ELSE;
                                        LOG("&e» &7Bancada de Trabalho fechada. &9&o#SLEEP");
                                    ENDIF;
                                ENDIF;
                            GUI();
                            WAIT(10T);
                            LOG("&4&l[Macro] &6Sleep_Craft &4[OFF]");                       
                            TOGGLE(sleep_macro);
                            STOP;                     
                        ELSE;
                            LOG("&e» &7%&name_minerio%&7's insuficiêntes. &9&o#SLEEP");
                            GUI();
                            TOGGLE(sleep_macro);
                            STOP;         
                        ENDIF;
                ENDIF;
            ELSE;
                LOG("&e» &7Não foi encontrado minérios no inventário. &9&o#SLEEP");
                GUI();
                WAIT(10T);
                LOG("&4&l[Macro] &6Sleep_Craft &4[OFF]");       
                TOGGLE(sleep_macro);
                STOP;
            ENDIF;
    ELSE;
        LOG("&4&l[Macro] &6Sleep_Craft &4[OFF]");
        STOP;
    ENDIF;

}$$