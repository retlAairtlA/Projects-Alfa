$${

TOGGLE(sleep_macro);
    IF(sleep_macro);
        LOG("&6&l[&f&oM&facro&6&l] &aProtect &a&oand &aWalk &f&l[&2ON&f&l]");   
            IF(!first);
                SET(first);
                LOG("");
                LOG("&6&l[&f&oI&fnfo&6&l] &aAo abrir o inventário, automaticamente a macro vai pegar a sua espada e defender. &9&o#SLEEP");
                LOG("&6&l[&f&oI&fnfo&6&l] &aSe você quiser correr com o inventário aberto basta pressionar as teclas &fW&a, &fA&a, &fS&a, &fD &aou &fESPAÇO&a. &9&o#SLEEP");
                LOG("");
            ENDIF;
    ELSE;
        LOG("&6&l[&f&oM&facro&6&l] &cProtect &c&oand &cWalk &f&l[&4OFF&f&l]");   
        KEYUP(JUMP);
        KEYUP(FORWARD);
        STOP;
    ENDIF;
    DO;
        UNSET(sleep_direcoes);
            IF(%GUI%!="NONE");
                SET(sleep_direcoes);
                UNSET(sleep_jump);
                    IF(%GUI%="GUIINVENTORY");
                        DO;UNTIL((!KEY_W)&&(!KEY_S)&&(!KEY_A)&&(!KEY_D)&&(!KEY_SPACE));
                        PICK(276,diamond_sword);
                            IFMATCHES("%ITEM%","(276|diamond_sword)");                      
                                LOG("&6&l[&f&oM&facro&6&l] &aProtect Mode ativado. &9&o#SLEEP");   
                                    DO;
                                        KEYUP(FORWARD);
                                        PICK(276,diamond_sword);
                                            IFMATCHES("%ITEM%","(276|diamond_sword)");
                                                KEY(USE);
                                            ENDIF;
                                            IF(KEY_SPACE);  
                                                TOGGLE(sleep_jump);
                                                BREAK;
                                            ENDIF;                              
                                    UNTIL((%GUI%!="GUIINVENTORY")||(KEY_W)||(KEY_S)||(KEY_A)||(KEY_D));
                            ENDIF;
                    ENDIF;
                    IF((sleep_direcoes)&&(%GUI%!="NONE"));
                        LOG("&6&l[&f&oM&facro&6&l] &aWalk Mode ativado. &9&o#SLEEP");   
                            IF(%DIRECTION%="N");
                                LOOK(NORTH);
                            ELSEIF(%DIRECTION%="S");
                                LOOK(SOUTH);
                            ELSEIF(%DIRECTION%="W");
                                LOOK(WEST);
                            ELSEIF(%DIRECTION%="E");
                                LOOK(EAST);
                            ENDIF;  
                        SET(sleep_first);
                            DO;
                                KEYDOWN(FORWARD);
                                SPRINT;
                                    IF(sleep_first);
                                        UNSET(sleep_first);
                                            DO;UNTIL((!KEY_W)&&(!KEY_S)&&(!KEY_A)&&(!KEY_D)&&(!KEY_SPACE));
                                    ELSE;
                                        IF(%GUI%!="GUICHAT");
                                            IF((KEY_W)||(KEY_S));
                                                LOOK(+180,+0);  
                                                    DO;UNTIL((!KEY_W)&&(!KEY_S));
                                            ELSEIF(KEY_A);
                                                LOOK(-90,+0);   
                                                    DO;UNTIL(!KEY_A);
                                            ELSEIF(KEY_D);
                                                LOOK(+90,+0);
                                                    DO;UNTIL(!KEY_D);
                                            ELSEIF(KEY_SPACE);      
                                                KEYDOWN(JUMP);
                                                TOGGLE(sleep_jump);
                                                    DO;UNTIL(!KEY_SPACE);
                                            ENDIF;
                                        ENDIF;
                                    ENDIF;
                                    IF(sleep_jump);
                                        KEYDOWN(JUMP);
                                    ELSE;
                                        KEYUP(JUMP);
                                    ENDIF;                                  
                            UNTIL(%GUI%="NONE");
                        KEYUP(FORWARD);
                        KEYUP(JUMP);
                    ENDIF;
            ENDIF;
    LOOP;

}$$