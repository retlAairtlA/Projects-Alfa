$${

SET(&pickupitem,"%PICKUPITEM%");
    IF((%&pickupitem%="Maçã Dourada")&&(%PICKUPDATA%=1));
        SET(&pickupitem,"CAPIROTO");
    ENDIF;
REPLACE(&pickupitem," ","_");
REPLACE(&pickupitem,"-","_");
    IFMATCHES("%@&itens_pickup%","<N:%&pickupitem%-T:[\d,.]+-Q:[\d,.]+>");
        MATCH("%@&itens_pickup%","<N:%&pickupitem%-T:([\d,.]+)-Q:([\d,.]+)>",{#sleep_time,#sleep_stack});
        #stack_new = %#sleep_stack% + %PICKUPAMOUNT%;
        REPLACE(@&itens_pickup,"<N:%&pickupitem%-T:%#sleep_time%-Q:%#sleep_stack%>","<N:%&pickupitem%-T:60-Q:%#stack_new%>");
    ELSE;
        SET(@&itens_pickup,"<N:%&pickupitem%-T:60-Q:%PICKUPAMOUNT%>%@&itens_pickup%");
    ENDIF;
    IF(!@pickup);
        SET(@pickup);
            DO;
                IFMATCHES("%@&itens_pickup%","<N:(.+)-T:[\d,.]+-Q:[\d,.]+>");
                    SET(#inc_break,"");
                    SET(&itens_copy,"%@&itens_pickup%");
                    SET(&sleep_label,"");
                        UNSAFE;
                            DO;
                                IFMATCHES("%&itens_copy%","<N:[A-Za-záàâãéèêíïóôõöúçñÁÀÂÃÉÈÍÏÓÔÕÖÚÇÑ')(._]+-T:[\d,.]+-Q:[\d,.]+>");
                                    MATCH("%&itens_copy%","<N:([A-Za-záàâãéèêíïóôõöúçñÁÀÂÃÉÈÍÏÓÔÕÖÚÇÑ')(._]+)-T:([\d,.]+)-Q:([\d,.]+)>(.*)",{&item_name,#item_time,#item_stack,&itens_copy});
                                    REPLACE(&item_name,"_"," ");
                                        IF(%#item_stack%=1);
                                            SET(&sleep_label,"%&sleep_label%&f%&item_name%\n");
                                        ELSE;
                                            SET(&sleep_label,"%&sleep_label%&f%&item_name% &c&ox&c%#item_stack%\n");
                                        ENDIF;
                                    REPLACE(&item_name," ","_");
                                        IF(%#item_time%>1);
                                            #item_time_new = %#item_time% - 1;
                                            REPLACE(@&itens_pickup,"<N:%&item_name%-T:%#item_time%-Q:%#item_stack%>","<N:%&item_name%-T:%#item_time_new%-Q:%#item_stack%>");
                                        ELSE;
                                            REPLACE(@&itens_pickup,"<N:%&item_name%-T:%#item_time%-Q:%#item_stack%>","");
                                        ENDIF;
                                ENDIF;
                            UNTIL(%&itens_copy%=NOTHING);
                        ENDUNSAFE;
                        IFMATCHES("%&sleep_label%","CAPIROTO &c&ox");
                            REPLACE(&sleep_label,"CAPIROTO","&lC4P1ROTO");
                        ELSE;
                            REPLACE(&sleep_label,"CAPIROTO","&lC4P1ROTO   ");
                        ENDIF;
                    SETLABEL("PICKUP","&6Você pegou:\n%&sleep_label%");
                ELSE;
                    INC(#inc_break,1);
                        IF(%#inc_break%>=10);
                            BREAK;
                        ELSE;
                            SETLABEL("PICKUP","&6Você pegou:");
                        ENDIF;
                ENDIF;
            LOOP;
        SETLABEL("PICKUP","");
        UNSET(@pickup);
    ENDIF;

}$$